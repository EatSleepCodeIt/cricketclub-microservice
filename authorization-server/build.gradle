apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'flyway'

sourceCompatibility = 1.8
targetCompatibility = 1.8
version='0.0.1-SNAPSHOT'
status = 'SNAPSHOT'
group = 'com.ratedforum.uaa'

buildscript {
	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/libs-snapshot' }
		maven { url 'https://repo.spring.io/plugins-snapshot' }
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.1.RELEASE'
		classpath 'io.spring.gradle:dependency-management-plugin:0.3.1.BUILD-SNAPSHOT'
		classpath "mysql:mysql-connector-java:5.1.34"
		classpath 'org.flywaydb:flyway-gradle-plugin:3.1'
		classpath "net.saliman:gradle-cobertura-plugin:2.2.5"
		classpath "org.ajoberstar:gradle-git:0.8.0"
	}
}

repositories {
	jcenter()
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
	maven { url "http://172.31.11.79/content/groups/public" }
	maven { url 'https://repo.spring.io/libs-milestone' }
	maven { url "https://repo.spring.io/libs-release" }
}

dependencyManagement {
	imports { mavenBom 'org.springframework.cloud:spring-cloud-versions:1.0.2.RELEASE' }
}

dependencies {
	compile "org.springframework.security.oauth:spring-security-oauth2:2.0.6.RELEASE"
	compile "org.springframework.boot:spring-boot-starter-data-jpa"
	compile 'org.springframework.cloud:spring-cloud-starter-eureka'
	compile 'org.springframework.cloud:spring-cloud-starter-hystrix-dashboard'
	compile 'org.springframework.security:spring-security-jwt:1.0.3.RELEASE'

	compile('mysql:mysql-connector-java:5.1.34')
	compile 'com.netflix.hystrix:hystrix-javanica:1.3.19'
	compile 'org.slf4j:slf4j-api:1.7.10'
	compile 'org.logback-extensions:logback-ext-loggly:0.1.2'
	compile 'ch.qos.logback.contrib:logback-json-classic:0.1.2'
	compile 'ch.qos.logback.contrib:logback-jackson:0.1.2'
	compile 'org.flywaydb:flyway-core:3.1'
}

task setupDockerFolder(type: Copy) {
	from('build/libs/')
	into('docker/')
	include('*.jar')
	rename { String fileName ->
		fileName.replace("-${version}", "")
	}
}

import org.apache.tools.ant.filters.*

afterEvaluate {
	configure(allProcessResourcesTasks()) {
		filter(ReplaceTokens,
			tokens: [baseName: project.jar.baseName,
					version: project.jar.version,
					gradleVersion: project.gradle.gradleVersion]
		)
	}
}

def allProcessResourcesTasks() {
	sourceSets*.processResourcesTaskName.collect {
		tasks[it]
	}
}

import org.ajoberstar.grgit.*

configure(rootProject) {
	task gitMetadata << {
		
		ext {
			repo = Grgit.open(project.file('.'))
			branch = repo.branch?.current?.name
			commitId = repo.head().abbreviatedId
			commitTime = new Date(new Integer(repo.head().time)
					.longValue()*1000L).format("yyyy-MM-dd HH:mm")
		}
	}

	apply from: 'writeGitPropertiesFile.gradle'
}
